plugins {
    id "com.gradleup.shadow"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
    
    // Globally exclude the problematic fabric-blockrenderlayer-v1 module
    all {
        exclude group: 'net.fabricmc.fabric-api', module: 'fabric-blockrenderlayer-v1'
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    modImplementation "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    modApi("cc.tweaked:cc-tweaked-${rootProject.cct_minecraft_version}-fabric-api:${rootProject.cct_version}")
    // Include CC: Tweaked mod for runtime testing (uncomment when available on Maven)
    // modRuntimeOnly("cc.tweaked:cc-tweaked-${rootProject.cct_minecraft_version}-fabric:${rootProject.cct_version}")

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

tasks.register('moveFabricBuilds') {
    doLast {
        delete fileTree("../build/release").matching {
            include "**/${rootProject.archives_base_name}-${rootProject.minecraft_version}-${rootProject.mod_version}-fabric*"
        }
        delete fileTree("../build/libs").matching {
            include "**/${rootProject.archives_base_name}-${rootProject.minecraft_version}-${rootProject.mod_version}-fabric*"
        }
        delete fileTree("../build/latest").matching {
            include "**/${rootProject.archives_base_name}-${rootProject.minecraft_version}-${rootProject.mod_version}-fabric*"
        }
        copy {
            from "build/libs"
            include "*${rootProject.mod_version}.jar"
            into "../build/release"
            rename "(.+)", "${rootProject.archives_base_name}-${rootProject.minecraft_version}-${rootProject.mod_version}-fabric.jar"
        }
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}

afterEvaluate {
    for (def task in it.tasks) {
        if (task == tasks.build) {
            task.finalizedBy tasks.moveFabricBuilds
        }
    }
}