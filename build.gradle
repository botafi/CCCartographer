plugins {
    id 'dev.architectury.loom' version '1.13.463' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
        implementation "net.kyori:adventure-api:4.26.1"
        compileOnly("cc.tweaked:cc-tweaked-${rootProject.cct_minecraft_version}-common-api:${rootProject.cct_version}")
        compileOnly "de.bluecolored:bluemap-api:${rootProject.bluemap_version}"
        // compileOnly "us.dynmap:DynmapCoreAPI:${rootProject.dynmap_version}"
        // compileOnly "maven.modrinth:pl3xmap:${rootProject.pl3xmap_version}"
        // compileOnly "xyz.jpenilla:squaremap-api:${rootProject.squaremap_version}"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name
    }
    version = "${rootProject.mod_version}"
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        mavenCentral()
        maven {
           name = "Squiddev maven cct"
           url "https://squiddev.cc/maven/"
           content {
             includeGroup("cc.tweaked")
             includeModule("org.squiddev", "Cobalt")
           }
        }
        maven { url 'https://jitpack.io' }
        maven { url = "https://repo.mikeprimm.com/" }
        maven { url 'https://repo.bluecolored.de/releases' }
        exclusiveContent {
            forRepository {
                maven {
                    url = "https://cursemaven.com"
                }
            }
            filter {
                includeGroup("curse.maven")
            }
        }
        exclusiveContent {
            forRepository { maven { url = "https://api.modrinth.com/maven" } }
            filter { includeGroup "maven.modrinth" }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}
