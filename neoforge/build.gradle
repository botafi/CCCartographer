plugins {
    id "com.gradleup.shadow"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"
    // Architectury API
    modApi "dev.architectury:architectury-neoforge:${rootProject.architectury_version}"

    // CC:Tweaked for NeoForge (using forge-api artifact name as that's what's available on maven)
    modApi("cc.tweaked:cc-tweaked-${rootProject.cct_minecraft_version}-forge-api:${rootProject.cct_version}")
    // Include CC: Tweaked mod for runtime testing (uncomment when available on Maven)
    // modRuntimeOnly("cc.tweaked:cc-tweaked-${rootProject.cct_minecraft_version}-forge:${rootProject.cct_version}")

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

tasks.register('moveNeoForgeBuilds') {
    doLast {
        delete fileTree("../build/release").matching {
            include "**/${rootProject.archives_base_name}-${rootProject.minecraft_version}-${rootProject.mod_version}-neoforge*"
        }
        delete fileTree("../build/libs").matching {
            include "**/${rootProject.archives_base_name}-${rootProject.minecraft_version}-${rootProject.mod_version}-neoforge*"
        }
        delete fileTree("../build/latest").matching {
            include "**/${rootProject.archives_base_name}-${rootProject.minecraft_version}-${rootProject.mod_version}-neoforge*"
        }
        copy {
            from "build/libs"
            include "*${rootProject.mod_version}.jar"
            into "../build/release"
            rename "(.+)", "${rootProject.archives_base_name}-${rootProject.minecraft_version}-${rootProject.mod_version}-neoforge.jar"
        }
    }
}

publishing {
    publications {
        mavenNeoForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    repositories {
        // Add repositories to publish to here.
    }
}

afterEvaluate {
    for (def task in it.tasks) {
        if (task == tasks.build) {
            task.finalizedBy tasks.moveNeoForgeBuilds
        }
    }
}
